/*

Notes on this job:
 - it will use an Artifactory to cache docker images an avoid building and building them from scratch
 - (because of the previous issue) several instances of this job cannot run in paralel (they might override the `latest` image uploaded)

*/

// Maybe parameterized somewhere
dockerUsername = 'center-c3i-docker.jfrog.io'
dockerhubUsername = 'conanio'

def buildImage(String latestMaster, String latestBranch, String expectedImage, String buildArgs, String target = null, String extraCacheFrom = null) {
    sh 'export DOCKER_BUILDKIT=0'

    withCredentials([usernamePassword(credentialsId: 'docker-credentials', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD'
        sh 'docker pull docker.io/library/ubuntu:artful'
    }

    withCredentials([usernamePassword(credentialsId: 'center-c3i-docker', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${dockerUsername}"
    }

    String cacheFrom = extraCacheFrom ? " --cache-from ${extraCacheFrom}" : ''
    if (0 == sh(script: "docker pull ${latestMaster}", returnStatus: true)) {
        cacheFrom = "${cacheFrom} --cache-from ${latestMaster}"
    }
    if (0 == sh(script: "docker pull ${latestBranch}", returnStatus: true)) {
        cacheFrom = "${cacheFrom} --cache-from ${latestBranch}"
    }

    sh "docker build ${cacheFrom} -t ${expectedImage} ${buildArgs} ${target ? "--target $target": ''} ."

    sh "docker push ${expectedImage}"
    sh "docker tag ${expectedImage} ${latestBranch}"
    sh "docker push ${latestBranch}"
}

def get_container_id() {
    // This is quite fragile and might depends on the Jenkins configuration and deployment, but it is the best
    //  I can figure out to get the ID of the running container to be able to mount the same volume when
    //  using docker-in-docker.
    String nodeName = "${NODE_NAME}"
    String output = sh(script: 'docker ps --no-trunc', returnStdout: true)
    String nodeId = null
    output.split('\n').each({
        String line = it.trim()
        if (line.contains(nodeName) && line.contains('/opt/entrypoint.sh')) {
            nodeId = line.split(' ')[0]
        }
    })
    echo "Node ${nodeName} with docker ID '${nodeId}'"
    return nodeId
}

def checkoutSources() {
    if (params.scm_pr_number) {
        echo "Install reference from SCM repository. PR ${params.scm_pr_number} merging with 'master' branch"
        // TODO: Try using extension 'PreBuildMerge' (https://gist.github.com/martinda/b2ece95c2c71ddb4d4a762f0a02561b3)
        checkout([$class           : 'GitSCM',
                    branches         : [[name: params.scm_pr_commit]],
                    userRemoteConfigs: [[name         : 'origin', refspec: "+refs/pull/${params.scm_pr_number}/head:refs/remotes/origin/PR-${params.scm_pr_number} +refs/heads/master:refs/remotes/origin/master",
                                        credentialsId: 'GITHUB_CLONE', url: params.scm_repository]],
        ])
        sh 'git config user.email "any@notused.com"'
        sh 'git config user.name "CI bot"'
        sh "git merge --no-ff ${params.scm_commit}"
    }
    else {
        echo 'Install reference from the SCM repository'
        checkout([$class           : 'GitSCM',
                    branches         : [[name: params.scm_commit]],
                    userRemoteConfigs: [[credentialsId: 'GITHUB_CLONE', url: params.scm_repository]],
        ])
    }
    sh 'git log -n 10 --graph --pretty=oneline --abbrev-commit --all --decorate=full'
}

boolean isLegacyJenkinsUsed(String compiler, String version) {
    // Returns if this legacy image is being used in Jenkins or not. We don't want to create them
    //  if they are not to be used in the future.
    int v = version as int
    if (compiler == 'gcc' && v >= 11) {
        echo 'Do not create (legacy) jenkins images for GCC >= 11'
        return false
    }
    else if (compiler == 'clang' && v >= 12) {
        echo 'Do not create (legacy) jenkins images for Clang >= 12'
        return false
    }
    return true
}

node('Linux') {
    List<String> gccVersions = params.gcc_versions.split('\n')
    List<String> clangVersions = params.clang_versions.split('\n')

    environment {
        DOCKER_BUILDKIT = 0
    }

    stage('Legacy - Input parameters') {
        echo """
        About sources:
        - scm_repository: ${params.scm_repository}
        - scm_commit: ${params.scm_commit}
        - scm_base_branch: ${params.scm_base_branch}
        - scm_pr_number: ${params.scm_pr_number}
        - scm_pr_commit: ${params.scm_pr_commit}
        """
        if (params.scm_pr_number) {
            currentBuild.description = "${params.suffix} | PR-${params.scm_pr_number} - ${params.scm_pr_commit.substring(0, Math.min(8, params.scm_pr_commit.size()))}"
        }
        else {
            currentBuild.description = "${params.suffix} | ${params.scm_base_branch} - ${params.scm_commit.substring(0, Math.min(8, params.scm_commit.size()))}"
        }
        // TODO: currentBuild.description = "${}"

        echo """
        About tools installed:
        - conan_version: ${params.conan_version}
        - jenkins_agent_version: ${params.jenkins_agent_version}
        """

        echo """
        Images to generate:
        - gcc_versions: ${gccVersions.join(', ')}
        - clang_versions: ${clangVersions.join(', ')}
        """
        assert gccVersions.size() > 0: 'Expecting to build some GCC versions at least'
        assert clangVersions.size() > 0: 'Expecting to build some Clang versions at least'

        echo """
        About docker images generated:
        - dockerUsername: ${dockerUsername}
        - dockerhubUsername: ${dockerhubUsername}
        """

        echo """
        Workflow control
        - upload_latest: ${params.upload_latest}
        - upload_main_images: ${params.upload_main_images}
        - upload_jenkins_images: ${params.upload_jenkins_images}
        - dry_run: ${params.dry_run}
        """
    }

    stage('Legacy - Checkout sources') {
        checkoutSources()
    }

    stage('Legacy - Install deps') {
        sh 'pip install docker-compose humanfriendly conan conan-package-tools'
    }

    // Get some global information, used later
    String suffix = params.suffix ? "-${params.suffix}" : ''
    String mainContainerID = get_container_id()
    String targetBranch = params.scm_base_branch.replaceAll('/', '-')
    String branchName = targetBranch
    if (params.scm_pr_number) {
        branchName = "PR-${params.scm_pr_number}"
    }

    boolean isMaster = (branchName == 'master')
    echo ">> branchName: ${branchName}, targetBranch: ${targetBranch} (isMaster: ${isMaster})"
    String commit = "n${env.BUILD_NUMBER}" // We need something unique for this execution, the commit is not enough (we can trigger again same commit)

    // Build build-args list
    Map<String, String> buildArguments = [:]
    Map<String, String> testArguments = [:]
    stage('Legacy - Build arguments') {
        buildArguments.put('CONAN_VERSION', params.conan_version)

        // TODO: Pass expected in a different way
        writeFile(file: 'legacy/.env', text: testArguments.collect({ k, v -> "${k }=${v }" }).join('\n') as String)
        sh 'cat legacy/.env'
    }
    String buildArgs = buildArguments.collect({ k, v -> "--build-arg ${k }=${v }" }).join(' ') as String

    // Execute login: 1) Validate the login 2) Needed for docker pull/push
    withCredentials([usernamePassword(credentialsId: 'center-c3i-docker', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${dockerUsername}"
    }

    // Define helper function to build the compiler
    Closure compilerBuild = { String compiler, String version ->
        return {
            String versionMajor = version

            String deployImage = "${dockerUsername}/${compiler}${versionMajor}:${branchName}-${commit}"
            String jenkinsImage = "${dockerUsername}/${compiler}${versionMajor}-jnlp-slave:${branchName}-${commit}"
            String compilerArgs = "${buildArgs}"

            node('Linux') {
                stage('Legacy - Checkout and install deps') {
                    checkoutSources()
                    sh 'cat legacy/.env'
                }

                stage("Legacy - ${compiler.toUpperCase()} ${version} deploy") {
                    String latestBranch = "${dockerUsername}/${compiler}${versionMajor}:${branchName}-latest"
                    String latestMaster = "${dockerUsername}/${compiler}${versionMajor}:${targetBranch}-latest"

                    dir("legacy/${compiler}_${versionMajor}") {
                        buildImage(latestMaster, latestBranch, deployImage, compilerArgs)
                    }
                }

                if (isLegacyJenkinsUsed(compiler, version)) {
                    stage("Legacy - ${compiler.toUpperCase()} ${version} jenkins") {
                        String latestBranch = "${dockerUsername}/${compiler}${versionMajor}-jnlp-slave:${branchName}-latest"
                        String latestMaster = "${dockerUsername}/${compiler}${versionMajor}-jnlp-slave:${targetBranch}-latest"

                        dir('legacy/jenkins-jnlp-slave') {
                            String jenkins_args = "--build-arg SOURCE_CONANIO_IMAGE=${deployImage} --build-arg AGENT_VERSION=${params.jenkins_agent_version}"
                            buildImage(latestMaster, latestBranch, jenkinsImage, "${compilerArgs} ${jenkins_args}")
                        }
                    }
                }
            }
        }
    }

    // Define helper function to build the compiler
    Closure appBuild = { String folder , String app ->
        return {
            String deployImage = "${dockerUsername}/${app}:${branchName}-${commit}"
            String compilerArgs = "${buildArgs}"

            node('Linux') {
                stage('Legacy - Checkout and install deps') {
                    checkoutSources()
                    sh 'cat legacy/.env'
                }

                stage("Legacy - ${app.toUpperCase()} ${folder} deploy") {
                    String latestBranch = "${dockerUsername}/${app}:${branchName}-latest"
                    String latestMaster = "${dockerUsername}/${app}:${targetBranch}-latest"

                    dir("legacy/${folder}") {
                        buildImage(latestMaster, latestBranch, deployImage, compilerArgs)
                    }
                }
            }
        }
    }

    // Build GCC in parallel
    Map gccBuilds = [:]
    List<String> gccImages = []
    gccVersions.each { v ->
        String gccVersion = v
        String gccVersionMajor = gccVersion
        gccImages.add("gcc${gccVersionMajor}")
        gccBuilds["Legacy GCC ${gccVersionMajor}"] = compilerBuild('gcc', gccVersion)
    }
    gccBuilds.failFast = true
    parallel(gccBuilds)

    // Build Clang in parallel
    Map clangBuilds = [:]
    List<String> clangImages = []
    clangVersions.each { v ->
        String clangVersion = v
        String clangVersionMajor = clangVersion
        clangImages.add("clang${clangVersionMajor}")
        clangBuilds["Legacy Clang ${clangVersionMajor}"] = compilerBuild('clang', clangVersion)
    }
    clangBuilds.failFast = true
    parallel(clangBuilds)

    if (params.build_conan_client) {
        Map conanClientBuilds = [:]
        conanClientBuilds['Legacy Conan Client'] = appBuild('conan_client', 'conan')
        conanClientBuilds.failFast = true
        parallel(conanClientBuilds)
    }

    if (params.build_conan_server) {
        Map conanServerBuilds = [:]
        conanServerBuilds['Legacy Conan Serve'] = appBuild('conan_server', 'conan_server')
        conanServerBuilds.failFast = true
        parallel(conanServerBuilds)
    }

    if (isMaster) {
        // If it was master and everything is ok up to here
        stage('Legacy - Upload images') {
            List images = []
            gccVersions.each { v ->
                String versionMajor = v
                if (params.upload_main_images) {
                    images.add("gcc${versionMajor}")
                }
                if (params.upload_jenkins_images && isLegacyJenkinsUsed('gcc', versionMajor)) {
                    images.add("gcc${versionMajor}-jnlp-slave")
                }
            }
            clangVersions.each { v ->
                String versionMajor = v
                if (params.upload_main_images) {
                    images.add("clang${versionMajor}")
                }
                if (params.upload_jenkins_images && isLegacyJenkinsUsed('clang', versionMajor)) {
                    images.add("clang${versionMajor}-jnlp-slave")
                }
            }
            if (params.build_conan_client) {
                images.add('conan')
            }
            if (params.build_conan_server) {
                images.add('conan_server')
            }

            images.each { image ->
                String builtImage = "${dockerUsername}/${image}:${branchName}-${commit}"
                sh "docker pull ${builtImage}"

                withCredentials([usernamePassword(credentialsId: 'docker-credentials', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
                    sh 'docker login --username $USERNAME --password $PASSWORD'
                }

                // Upload with the corresponding tag version
                String uploadImage = "${dockerhubUsername}/${image}:${params.conan_version}"
                sh "docker tag ${builtImage} ${uploadImage}"
                if (!params.dry_run) {
                    sh "docker push ${uploadImage}"
                }

                if (params.upload_latest) {
                    // Upload with tag 'latest'
                    String uploadLatest = "${dockerhubUsername}/${image}:latest"
                    sh "docker tag ${builtImage} ${uploadLatest}"
                    if (!params.dry_run) {
                        sh "docker push ${uploadLatest}"
                    }
                }
            }
        }
    }
}
