
def detectPullRequestMerged() {
    stage("Detect Pull Request merged") {
        String prNumber = ""
        def commitMessage = sh(returnStdout: true, script: 'git show -s --format=%s').trim()
        // WARN: This is very, very fragile... if someone changes the message, if it contains # at some point...
        def regex = /(.*?)#(\d+)(.*)/
        def findPR = (commitMessage =~ /$regex/)
        if (findPR.size() != 1) {
            return null
        }
        return findPR[0][2]
    }
}

def readEnvValues(String envFile) {
    Map<String, String> envContent = [:]
    String content = readFile(envFile)  // legacy/.env or modern/.env
    echo content
    content.split('\n').each({ it.trim() }).findAll({ it.size() && !it.startsWith('#') }).each {
        def (name, value) = it.split('=')
        echo "${name}=${value}"
        envContent[name] = value
    }
    return envContent
}

def parseVersion(String version) {
    def matcher = (version =~ /(\d+).(\d+).(\d+)(\w{1}\d+)?/)
    return [matcher[0][1] as String, matcher[0][2] as String, matcher[0][3] as String]
}

String getVersionMajor(String version) {
    def (major, _1, _2) = parseVersion(version)
    return major
}

String getImageMasterName(Map image) {
    String conanVersion = image["buildArgs"]["CONAN_VERSION"]
    String pr_number = image["pr_number"]
    return image["name"].replace("-PR${pr_number}", "").replace(conanVersion , "master")
}

def buildLegacyImage(Map image, String buildArgs, String registry, String registryCredentialsID, String cacheFrom) {
    withCredentials([usernamePassword(credentialsId: registryCredentialsID, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${registry}"
        image["pull"].each {
            sh "docker pull ${it} || true"
        }
    }

    sh "docker build -t ${image["name"]} ${cacheFrom} ${buildArgs} ."
}

def uploadImage(Map image, String fromRegistryName, String toRegistryName, String crendentialsID) {
    withCredentials([usernamePassword(credentialsId: crendentialsID, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${toRegistryName}"
    }

    // Upload with the corresponding tag version.
    // image["name"] can or not contain the registry name
    String imageName = image["name"].replace("${fromRegistryName}/", "")
    String uploadImageName = "${toRegistryName}/${imageName}"
    String localImageName = "${fromRegistryName}/${imageName}"
    if (localImageName != uploadImageName) {
        sh "docker tag ${localImageName} ${uploadImageName}"
    }
    sh "docker push ${uploadImageName}"
}

def getLegacyImageName(Map imageData, String registry) {
    // <registry>/<docker_username>/<compiler><compilerVersion>(-<suffix>):<conan_version>-PR<pr_number>
    String name = "${imageData["dockerUsername"]}/${imageData["compiler"]}${imageData["compilerVersion"]}"
    name = imageData["suffix"] != null ? "${name}-${imageData["suffix"]}" : name
    String tag = "${imageData["buildArgs"]["CONAN_VERSION"]}-PR${imageData["pr_number"]}"
    return "${registry}/${name}:${tag}"
}

def getLegacyImages(String pr_number, String registry) {
    Map<String, String> envValues = [:]
    stage('Legacy - Parse environment file') {
        envValues = readEnvValues("legacy/.env")
    }

    List<Map> images = []
    stage('Legacy - Generate build matrix') {
        List<String> gccVersions = envValues["GCC_LEGACY_VERSIONS"].split(",")
        Map baseDockerImage = ["5": "docker.io/library/ubuntu:xenial", "7": "docker.io/library/ubuntu:bionic", "9": "docker.io/library/ubuntu:eoan"]

        gccVersions.each({ String gccVersion ->
            String dockerUsername = envValues["DOCKER_USERNAME"]
            String image = "${dockerUsername}/gcc${gccVersion}"
            Map imageData = ["suffix": null, "buildArgs": [:], "target": null, "prefix": null, "cache": [], "pull": []]
            imageData["dockerUsername"] = dockerUsername
            imageData["compiler"] = "gcc"
            imageData["compilerVersion"] = gccVersion
            imageData["pr_number"] = pr_number
            imageData["path"] = "legacy/gcc_${gccVersion}"
            imageData["buildArgs"]["CONAN_VERSION"] = envValues["CONAN_VERSION"]
            imageData["buildArgs"]["BUILDKIT_INLINE_CACHE"] = "1" // Enable BuildKit cache
            imageData["name"] = getLegacyImageName(imageData, registry)
            // pull images from registry to be used as cache
            String masterImageName = getImageMasterName(imageData)
            imageData["cache"] = [imageData["name"], masterImageName]
            imageData["pull"] = [imageData["name"], masterImageName, baseDockerImage[gccVersion]]
            images.add(imageData)

            Map jenkinsImageData = imageData.clone()
            jenkinsImageData["suffix"] = "jenkins"
            jenkinsImageData["buildArgs"]["SOURCE_CONANIO_IMAGE"] = imageData["name"]
            jenkinsImageData["buildArgs"]["JENKINS_AGENT_VERSION"] = envValues["JENKINS_AGENT_VERSION"]
            jenkinsImageData["buildArgs"]["JAVA_JDK_VERSION"] = envValues["JAVA_JDK_VERSION"]
            jenkinsImageData["path"] = "legacy/jenkins"
            jenkinsImageData["name"] = getLegacyImageName(jenkinsImageData, registry)
            // pull images from registry to be used as cache
            String jenkinsImageMasterName = getImageMasterName(jenkinsImageData)
            jenkinsImageData["cache"] = [jenkinsImageData["name"], jenkinsImageMasterName]
            jenkinsImageData["pull"] = [jenkinsImageData["name"], imageData["name"], jenkinsImageMasterName]
            images.add(jenkinsImageData)
        })
    }
    return images
}

def getModernImageName(Map imageData, String registry) {
    if (imageData["prefix"] == "base") {
        // base: <registry>/<docker_username>/<prefix>-<distro>:<conan_version>-PR<pr_number>
        String name = "${imageData["dockerUsername"]}/${imageData["prefix"]}-${imageData["distro"]}"
        String tag = "${imageData["buildArgs"]["CONAN_VERSION"]}-PR${imageData["pr_number"]}"
        return "${registry}/${name}:${tag}"
    } else {
        // builder/deploy/jenkins: <registry>/<docker_username>/<compiler><compilerMajorVersion>-<distro>(-<suffix>):<conan_version>(_<jenkins_agent>)-PR<pr_number>
        String compilerMajorVersion = getVersionMajor(imageData["compilerVersion"])
        String name = "${imageData["dockerUsername"]}/${imageData["compiler"]}${compilerMajorVersion}-${imageData["buildArgs"]["DISTRO"]}"
        name = imageData["suffix"] != null ? "${name}-${imageData["suffix"]}" : name
        String conanVersion = imageData["buildArgs"]["CONAN_VERSION"]
        String tag = imageData["suffix"] == "jenkins" ? "${conanVersion}-${imageData["buildArgs"]["JENKINS_AGENT_VERSION"]}" : conanVersion
        tag = "${tag}-PR${imageData["pr_number"]}"
        return "${registry}/${name}:${tag}"
    }
}

def defineModernBaseImageData(Map envValues, String pr_number, String registry) {
    Map imageData = ["buildArgs": [:], "target": null, "suffix": null, "prefix": null , "pull": [], "cache": []]
    imageData["prefix"] = "base"
    imageData["path"] = "modern/base"
    imageData['distro'] = envValues["DISTRO"]
    imageData["dockerUsername"] = envValues["DOCKER_USERNAME"]
    imageData["pr_number"] = pr_number
    imageData["buildArgs"]["BUILDKIT_INLINE_CACHE"] = "1"
    imageData["buildArgs"]['DISTRO_VERSION'] = envValues["DISTRO_VERSION"]
    imageData["buildArgs"]["CONAN_VERSION"] = envValues["CONAN_VERSION"]
    imageData["buildArgs"]["CMAKE_VERSION"] = envValues["CMAKE_VERSION"]
    imageData["buildArgs"]['PYTHON_VERSION'] = envValues["PYTHON_VERSION"]
    imageData["name"] = getModernImageName(imageData, registry)
    // pull images from registry to be used as cache
    String masterImageName = getImageMasterName(imageData)
    imageData["cache"] = [imageData["name"], masterImageName]
    imageData["pull"] = ["ubuntu:${envValues["DISTRO_VERSION"]}", imageData["name"], masterImageName]
    return imageData
}

def defineModernImagesData(String compiler, String compilerVersion, Map envValues, String pr_number, String registry) {
    List<Map> images = []

    Map builderImage = ["buildArgs": [:], "target": null, "suffix": null, "prefix": null, "pull": [], "cache": []]
    builderImage["compiler"] = compiler
    builderImage["compilerVersion"] = envValues["${compiler.toUpperCase()}${compilerVersion}_VERSION"]
    builderImage["pr_number"] = pr_number
    builderImage["dockerUsername"] = envValues["DOCKER_USERNAME"]
    builderImage['distro'] = envValues["DISTRO"]
    builderImage["buildArgs"]["BUILDKIT_INLINE_CACHE"] = "1" // Enable BuildKit cache
    builderImage["buildArgs"]["CMAKE_VERSION"] = envValues["CMAKE_VERSION"]
    builderImage["buildArgs"]['PYTHON_VERSION'] = envValues["PYTHON_VERSION"]
    builderImage["buildArgs"]['LIBSTDCPP_VERSION'] = envValues["LIBSTDCPP_VERSION"]
    builderImage["buildArgs"]['LIBSTDCPP_PATCH_VERSION'] = envValues["LIBSTDCPP_PATCH_VERSION"]
    builderImage["buildArgs"]['LIBSTDCPP_MAJOR_VERSION'] = envValues["LIBSTDCPP_MAJOR_VERSION"]
    builderImage["buildArgs"]['DOCKER_USERNAME'] = "${registry}/${envValues["DOCKER_USERNAME"]}"  // Use registry name to pull image from the right registry
    builderImage["buildArgs"]['DOCKERHUB_USERNAME'] = envValues["DOCKERHUB_USERNAME"]
    builderImage["buildArgs"]['DISTRO'] = envValues["DISTRO"]
    builderImage["buildArgs"]['DISTRO_VERSION'] = envValues["DISTRO_VERSION"]
    builderImage["buildArgs"]["CONAN_VERSION"] = envValues["CONAN_VERSION"]
    builderImage["buildArgs"]['DOCKER_TAG'] = "${envValues["CONAN_VERSION"]}-PR${pr_number}"
    builderImage["buildArgs"]["${compiler.toUpperCase()}_VERSION"] = envValues["${compiler.toUpperCase()}${compilerVersion}_VERSION"]
    builderImage["path"] = "modern/${compiler}"
    builderImage["suffix"] = "builder"
    builderImage["target"] = "builder"
    builderImage["name"] = getModernImageName(builderImage, registry)
    // pull images from registry to be used as cache
    String builderImageMasterName = getImageMasterName(builderImage)
    builderImage["pull"] = ["ubuntu:${envValues["DISTRO_VERSION"]}", builderImage["name"], builderImageMasterName]
    builderImage["cache"] = [builderImage["name"], builderImageMasterName]
    images.add(builderImage)

    Map deployImage = builderImage.clone()
    deployImage["target"] = "deploy"
    deployImage["suffix"] = null
    deployImage["name"] = getModernImageName(deployImage, registry)
    // pull images from registry to be used as cache
    Map baseImage = defineModernBaseImageData(envValues, pr_number, registry)
    String deployImageMasterName = getImageMasterName(deployImage)
    deployImage["cache"] = [deployImage["name"], "${registry}/${deployImageMasterName}"]
    deployImage["pull"] = [baseImage["name"], builderImage["name"], deployImage["name"], deployImageMasterName]
    images.add(deployImage)

    Map jenkinsImage = deployImage.clone()
    jenkinsImage["target"] = null
    jenkinsImage["suffix"] = "jenkins"
    jenkinsImage["buildArgs"]["SOURCE_CONANIO_IMAGE"] = deployImage["name"]
    jenkinsImage["buildArgs"]["JENKINS_AGENT_VERSION"] = envValues["JENKINS_AGENT_VERSION"]
    jenkinsImage["buildArgs"]["JAVA_JDK_VERSION"] = envValues["JAVA_JDK_VERSION"]
    jenkinsImage["path"] = "modern/jenkins"
    jenkinsImage["name"] = getModernImageName(jenkinsImage, registry)
    // pull images from registry to be used as cache
    String jenkinsImageMasterName = getImageMasterName(jenkinsImage)
    jenkinsImage["pull"] = [deployImage["name"], jenkinsImage["name"], jenkinsImageMasterName]
    jenkinsImage["cache"] = [jenkinsImage["name"], jenkinsImageMasterName]

    images.add(jenkinsImage)

    return images
}

def getModernImages(String pr_number, String registry) {
    Map<String, String> envValues = [:]
    stage('Modern - Parse environment file') {
        envValues = readEnvValues("modern/.env")
    }

    List<Map> images = []
    stage('Modern - Generate build matrix') {
        List<String> gccVersions = envValues["GCC_VERSIONS"].split(",").collect({String version -> return getVersionMajor(version)})
        List<String> clangVersions = envValues["CLANG_VERSIONS"].split(",").collect({String version -> return getVersionMajor(version)})

        images.add(defineModernBaseImageData(envValues, pr_number, registry))

        gccVersions.each({ String gccVersion ->
            images.addAll(defineModernImagesData("gcc", gccVersion, envValues, pr_number, registry))
        })

        clangVersions.each({ String clangVersion ->
            images.addAll(defineModernImagesData("clang", clangVersion, envValues, pr_number, registry))
        })
    }
    return images
}

def buildLegacyImages(List<Map> images, String registry, String registryCredentialsID) {
    // Define helper function to build the image
    Closure imageBuild = { Map image ->
        return {
            node('Linux') {
                String buildArgsStr = image["buildArgs"].collect({ k, v -> "--build-arg ${k }=${v }" }).join(' ') as String
                String cacheFrom = image["cache"].collect({"--cache-from=${it}"}).join(' ') as String

                environment {
                    DOCKER_BUILDKIT = 1
                    COMPOSE_DOCKER_CLI_BUILD = 0
                }

                stage('Legacy - Checkout sources') {
                    checkout scm
                }

                stage("Legacy - Build image ${image["name"]}") {
                    dir(image["path"]) {
                        buildLegacyImage(image, buildArgsStr, registry, registryCredentialsID, cacheFrom)
                    }
                }

                stage("Legacy - Upload image ${image["name"]} to Artifactory") {
                    uploadImage(image, registry, registry, registryCredentialsID)
                }
            }
        }
    }

    // Build images in parallel
    List<Map> normalImages = images.findAll({ Map image -> !image["name"].contains("jenkins") })
    List<Map> jenkinsImages = images.findAll({ Map image -> image["name"].contains("jenkins") })
    buildParallel("Legacy image", normalImages, imageBuild)
    buildParallel("Legacy image jenkins", jenkinsImages, imageBuild)
}

def buildParallel(String stageName, List images, Closure closure) {
    Map builds = [:]
    images.each { Map image ->
        builds["${stageName} ${image['name']}"] = closure(image)
    }
    builds.failFast = true
    parallel(builds)
}

def buildModernImage(Map image, String buildArgs, String target, String registry, String registryCredentialsID, String cacheFrom) {
    withCredentials([usernamePassword(credentialsId: registryCredentialsID, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${registry}"
        image["pull"].each {
            sh "docker pull ${it} || true"
        }
    }

    sh "docker build -t ${image["name"]} ${cacheFrom} ${buildArgs} ${target} ."
}

def buildModernImages(List<Map> images, String registry, String registryCredentialsID) {
    // Define helper function to build the compiler
    Closure imageBuild = { Map image ->
        return {
            node('Linux') {
                String buildArgsStr = image["buildArgs"].collect({ k, v -> "--build-arg ${k }=${v }" }).join(' ') as String
                String target = image["target"] != null ? "--target ${image["target"]}" : ""
                String cacheFrom = image["cache"].collect({"--cache-from=${it}"}).join(' ') as String

                environment {
                    DOCKER_BUILDKIT = 1
                    COMPOSE_DOCKER_CLI_BUILD = 0
                }

                stage('Modern - Checkout sources and install deps') {
                    checkout scm
                    // sh 'pip install pytest'
                    // TODO: Pass expected in a different way
                    // writeFile(file: 'modern/.env', text: testArguments.collect({ k, v -> "${k }=${v }"}).join('\n') as String)
                    // sh 'cat modern/.env'
                }

                stage("Modern - Build image ${image["name"]}") {
                    dir(image["path"]) {
                        buildModernImage(image, buildArgsStr, target, registry, registryCredentialsID, cacheFrom)
                    }
                }

                stage("Modern - Upload image ${image["name"]} to Artifactory") {
                    uploadImage(image, registry, registry, registryCredentialsID)
                }
            }
        }
    }

    // Build images in parallel
    // base, builder, normal, jenkins
    List<Map> baseImages = images.findAll({ Map image -> image["name"].contains("base") })
    List<Map> builderImages = images.findAll({ Map image -> image["name"].contains("builder") })
    List<Map> deployImages = images.findAll({ Map image -> image["target"] == "deploy" })
    List<Map> jenkinsImages = images.findAll({ Map image -> image["name"].contains("jenkins") })
    buildParallel("Modern base image", baseImages, imageBuild)
    buildParallel("Modern builder image", builderImages, imageBuild)
    buildParallel("Modern deploy image", deployImages, imageBuild)
    buildParallel("Modern jenkins image ", jenkinsImages, imageBuild)
}

def promoteImages(List<Map> images, String fromRegistryName, String fromRegistryCredentialsId,
                  String toRegistryName, String toRegistryCredentialsId) {
    withCredentials([usernamePassword(credentialsId: fromRegistryCredentialsId, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${fromRegistryName}"
    }

    images.each { Map image ->
        // Download from registry
        // TODO: We need to add a retry here as we may face networking errors.
        sh "docker pull ${image["name"]}"
        String imageName = image["name"].replace("${fromRegistryName}/", "")
        image["name"] = imageName
    }

    images.each { Map image ->
       uploadImage(image, fromRegistryName, toRegistryName, toRegistryCredentialsId)
    }
}

node('Linux') {
    String branch_commit = null
    String merge_commit = null
    String pr_number = null
    String pr_base_branch = null
    boolean isMasterbranch = false

    stage('Get build job information') {
        def scmVars = checkout scm
        merge_commit = scmVars.GIT_COMMIT
        branch_commit = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
        isMasterbranch = env.BRANCH_NAME.startsWith('master')  // Because it is Github
        if (!isMasterbranch) {  // It is a pull-request
            pr_number = env.BRANCH_NAME.replace('PR-', '')
            pr_base_branch = "${env.CHANGE_TARGET}"
        } else {
            pr_number = detectPullRequestMerged()
        }
        assert pr_number != null, "Could not detect pull request number"

        echo """
            Build job information:
            - branch_commit: ${branch_commit}
            - merge_commit: ${merge_commit}
            - pr_number: ${pr_number}
            - pr_base_branch: ${pr_base_branch}
            """

        sh "docker --version"
    }

    // Check for skip build
    String scmTitle = sh(script: 'git log -1 --pretty=%B', returnStdout: true).trim()
    if (scmTitle.contains('[skip ci]') || scmTitle.contains('[ci skip]')) {
        currentBuild.result = 'SUCCESS'  // Keep success, so GitHub shows green status check
        echo 'Aborting: Found [skip ci] on the commit message'
        return  // Outside stage
    }

    List<Map> legacyImages = getLegacyImages(pr_number, "center-c3i-docker.jfrog.io")
    echo "Legacy images: ${legacyImages}"
    List<Map> modernImages = getModernImages(pr_number, "center-c3i-docker.jfrog.io")
    echo "Modern images: ${modernImages}"

    if (!isMasterbranch) {
        // Build Legacy Images
        buildLegacyImages(legacyImages, "center-c3i-docker.jfrog.io", "center-c3i-docker")

        // Build Modern Images
        buildModernImages(modernImages, "center-c3i-docker.jfrog.io", "center-c3i-docker")
    }

    if (isMasterbranch) {
        // Tag images as definitive (without pr_number)
        legacyImages = tagImagesWithoutPRNumber(legacyImages, pr_number, "center-c3i-docker.jfrog.io", "center-c3i-docker")
        modernImages = tagImagesWithoutPRNumber(modernImages, pr_number, "center-c3i-docker.jfrog.io", "center-c3i-docker")

        // Filter images to promote in hub.docker only
        List<Map> legacyDeployImages = legacyImages.findAll({ Map image -> image["name"].contains("jenkins") || image["target"] == "deploy" })
        List<Map> modernDeployImages = legacyImages.findAll({ Map image -> image["name"].contains("jenkins") || image["target"] == "deploy" })

        // Promote to dockerhub
        promoteImages(legacyDeployImages, "center-c3i-docker.jfrog.io", "center-c3i-docker", "docker.io", "docker-credentials")
        promoteImages(modernDeployImages, "center-c3i-docker.jfrog.io", "center-c3i-docker", "docker.io", "docker-credentials")

        // TODO: Clean images from PR that are in Artifactory
    }
}

def tagImagesWithoutPRNumber(List<Map> images, String pr_number, String registry, String credentialsID) {
    withCredentials([usernamePassword(credentialsId: credentialsID, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
        sh 'docker login --username $USERNAME --password $PASSWORD ' + "${registry}"
    }

    images.each { Map image ->
        // Pull image from desired registry
        sh "docker pull ${image["name"]}"
        // Remove pr_number from image tag
        String imageWithoutPRNumber = getImageMasterName(image)
        echo "Tagging ${image["name"]} as ${imageWithoutPRNumber}"
        // Tag image without pr_number and push it to desired registry
        sh "docker tag ${image["name"]} ${imageWithoutPRNumber}"
        try {
            sh "docker push ${imageWithoutPRNumber}"
        } catch (Exception e) {
            echo "Docker push failed. Retrying..."
            sh "docker push ${imageWithoutPRNumber}"
        }
        // Return the image without pr_number (and without registry)
        image["name"] = imageWithoutPRNumber
    }
    return images
}
