/*

This pipeline is used to generate docker images for ConanCenter index

*/


@NonCPS
def getMinConanVersion(content) {
    def minVersion = (content =~ /  v1_required: (\d+).(\d+).(\d+)/)
    assert minVersion, 'configs/library_requirements.yml file was changed! Cannot find conanVersion'
    return [minVersion[0][1] as int, minVersion[0][2] as int, minVersion[0][3] as int]
}

def parseVersion(String version) {
    def matcher = (version =~ /(\d+).(\d+).(\d+)/)
    return [matcher[0][1] as int, matcher[0][2] as int, matcher[0][3] as int]
}

List<String> parseParamVersions(String versions) {
    List<String> result = []
    versions.split(',').each { v ->
        String version = v.trim()
        if (version.size() > 0) {
            result.add(version)
        }
    }
    return result
}

node('Linux') {

    environment {
        DOCKER_BUILDKIT = 0
    }

    stage('Input parameters') {
        echo """
        - dry_run: ${params.dry_run}
        - conan_versions: ${params.conan_versions}
        """
    }

    def conanVersions = []
    def latestVersion = null
    stage('Legacy - Compute Conan versions') {
        if (params.conan_versions) {
            List parsedVersions = parseParamVersions(params.conan_versions)
            // INFO: Check that all versions are valid
            conanVersions = parsedVersions.collect{ parseVersion(it) }
            echo "INFO: Skipping Conan versions computation because of input parameter."
            return
        }

        dir('tmp') {
            // Minimum from https://github.com/conan-io/c3i_jenkins/blob/master/.ci/tests.jenkins
            echo "INFO: Read lowest version from CCI requirements"
            checkout([$class           : 'GitSCM',
                    branches         : [[name: 'master']],
                    userRemoteConfigs: [[credentialsId: 'conanci-gh-token', url: 'https://github.com/conan-io/c3i_jenkins.git']],
                    extensions       : [[$class           : 'RelativeTargetDirectory',
                                        relativeTargetDir: 'c3i_jenkins']],
            ])
            def content = readFile "${env.WORKSPACE}/tmp/c3i_jenkins/resources/org/jfrog/c3i/configs/library_requirements.yml"
            def minVersion = getMinConanVersion(content)
            echo " - lowest: $minVersion"

            // Up to latest version in pypi
            echo "Read latest version from pypi"
            def response = httpRequest(url: 'https://pypi.org/pypi/conan/json')
            Map<String, Object> requestJson = readJSON(text: response.content)
            latestVersion = parseVersion(requestJson.info.version)
            def keyVersions = requestJson.releases.keySet()

            echo "INFO: Latest version available in pypi: ${latestVersion}"
            echo "INFO: Found versions in pypi: ${keyVersions.size()}"

            for (String release in keyVersions) {
                try {
                    echo "DEBUG: Parsing release '$release'"
                    def version = parseVersion(release)

                    if (version[0] == 2) {
                        echo "WARNING: The version '$release' is part of Conan 2.0 which will not be built for legacy images."
                        continue
                    }
                    else if (version[0] > minVersion[0]) {
                        echo "DEBUG: Adding '$release' to the list of versions because of major version."
                        conanVersions.add(version)
                    }
                    else if (version[0] == minVersion[0] && version[1] > minVersion[1]) {
                        echo "DEBUG: Adding '$release' to the list of versions because of minor version."
                        conanVersions.add(version)
                    }
                    else if (version[0] == minVersion[0] && version[1] == minVersion[1] &&  version[2] >= minVersion[2]) {
                        echo "DEBUG: Adding '$release' to the list of versions because of patch version."
                        conanVersions.add(version)
                    }
                }
                catch(e) {
                    echo "Failed to parse release '$release'"
                }
            }
        }
    }
    echo "INFO: Conan versions: ${conanVersions}"
    echo "INFO: Conan latest version: ${latestVersion}"

    List parameters = []

    // CCI uses same hardcoded values from the repository for the tools (only Conan version is changed)
    stage('Legacy - Parse environment file') {
        checkout scm
        List<String> gccVersions = ["5", "7", "9"]
        List<String> clangVersions = []

        parameters.add([$class: 'StringParameterValue', name: 'gcc_versions', value: "${gccVersions.join('\n')}"])
        parameters.add([$class: 'StringParameterValue', name: 'clang_versions', value: "${clangVersions.join('\n')}"])
    }

    // HEAD from 'master' branch
    String scm_commit = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
    parameters.add([$class: 'StringParameterValue', name: 'scm_repository', value: 'https://github.com/conan-io/conan-docker-tools'])
    parameters.add([$class: 'StringParameterValue', name: 'scm_commit', value: scm_commit])
    parameters.add([$class: 'StringParameterValue', name: 'scm_base_branch', value: 'master'])

    // [$class: 'StringParameterValue', name: 'suffix', value: 'cci'],
    parameters.add([$class: 'BooleanParameterValue', name: 'upload_main_images', value: true])
    parameters.add([$class: 'BooleanParameterValue', name: 'upload_jenkins_images', value: true])
    parameters.add([$class: 'BooleanParameterValue', name: 'dry_run', value: params.dry_run])

    for (v in conanVersions) {
        String strVersion = "${v[0]}.${v[1]}.${v[2]}"
        boolean isLatest = (v == latestVersion)
        stage("Build legacy images for Conan '${strVersion}' (latest=${isLatest})") {
            // INFO: When building docker images, networking errors are frequent. We retry 3 times.
            retry(3) {
                build(job: 'ConanDockerTools/_generate_legacy', propagate: true, wait: true, parameters: parameters + [
                    [$class: 'StringParameterValue', name: 'conan_version', value: strVersion],
                    [$class: 'BooleanParameterValue', name: 'upload_latest', value: isLatest],
                ])
            }
        }
    }
}
